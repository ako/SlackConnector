/* use:
 * Download all dependencies - \programs\gradle-3.5\bin\gradle getTestRuntimeLibs
 * Download just the export dependencies -  \programs\gradle-3.5\bin\gradle getExportLibs
 */

apply plugin: 'java'

configurations {
    export {}
    compile { extendsFrom export }
}
compileJava.destinationDir = file("$rootDir/deployment/run/bin")

dependencies {
    // public mendix runtime jars
    compile name: 'com.mendix.public-api'
    compile name: 'com.mendix.json'
    compile name: 'com.mendix.logging-api'
    compile name: 'com.mendix.m2ee-api'
    compile name: 'javax.servlet-api.servlet'
    // unit test module
    testRuntime 'junit:junit:4.12'
    testRuntime 'org.slf4j:slf4j-simple:1.7.5'
    testRuntime 'ca.szc.configparser:java-configparser:0.2'

    // for appcloudservices
    testRuntime 'org.openid4java:openid4java:1.0.0'
    testRuntime 'commons-lang:commons-lang:2.5'
    testRuntime 'org.apache.ws.commons.axiom:axiom-api:1.2.19'
    // export slack client sdk jars for upload to AppStore
    export group: 'com.ullink.slack', name: 'simpleslackapi', version: '1.2.0'

}

repositories {
    mavenCentral()
    maven { url 'http://packages.confluent.io/maven/' }
    flatDir {
        dirs '/c://Program Files/Mendix/7.23.3.48173/runtime/bundles'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['javasource']
        }
        resources {
            srcDirs = ['resources']
        }
    }
}

task getExportlibs(type: Copy) {
    doFirst {
        delete fileTree(dir: "userlib")
    }
    from configurations.export
    into 'userlib/'
    doLast {
        files { file('userlib').listFiles() }.each { File file ->
            println "file found: $file.name"
            def keepFilename = 'userlib/' + file.name + '.SlackConnector.RequiredLib'
            ant.touch(file: keepFilename)
        }
    }
}

task getTestRuntimelibs(type: Copy) {
    from configurations.testRuntime
    into 'userlib/'
}
